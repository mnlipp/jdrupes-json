apply plugin: 'eclipse'
    
eclipse {
    project {
        natures 'org.eclipse.buildship.core.gradleprojectnature', \
            'org.eclipse.jdt.core.javanature', \
            'net.sf.eclipsecs.core.CheckstyleNature', \
            'org.eclipse.jdt.groovy.core.groovyNature'
        buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
        buildCommand 'org.eclipse.jdt.core.javabuilder'
        buildCommand 'net.sf.eclipsecs.core.CheckstyleBuilder'
        if (project.name.startsWith('org.jdrupes.')) {
            project.natures += 'ch.acanda.eclipse.pmd.builder.PMDNature'
            project.buildCommand 'ch.acanda.eclipse.pmd.builder.PMDBuilder'
        }
    }
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
    jdt {
        javaRuntimeName = "JavaSE-11"
        file {
            withProperties { properties ->
                def formatterPrefs = new Properties()
                rootProject.file("gradle/org.eclipse.jdt.core.formatter.prefs")
                    .withInputStream { formatterPrefs.load(it) }
                properties.putAll(formatterPrefs)
            }
        }
    }
}
tasks.eclipse.dependsOn(cleanEclipse)
